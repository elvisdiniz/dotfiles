# make sure the re_match_pcre option is enabled in FreeBSD systems
# the precompiled version of zsh does not use pcre, needs to build and compile with the module:
# 
# how to build a a package from ports: https://www.cyberciti.biz/faq/install-ports-in-freebsd/
# zvm bug: https://github.com/jeffreytse/zsh-vi-mode/issues/159
if [ "$options[re_match_pcre]" = "off" ] && [ $(uname -s) = "FreeBSD" ]; then
    setopt re_match_pcre
fi
######################################################################################################################

# Create some bin symbolic links: fdfind => fd, batcat => bat, exa => eza
[ -x "$(command -v fdfind)" ] && [ ! -L ~/.local/bin/fd ] && ln -sf "$(command -v fdfind)" ~/.local/bin/fd
[ -x "$(command -v batcat)" ] && [ ! -L ~/.local/bin/bat ] && ln -sf "$(command -v batcat)" ~/.local/bin/bat
[ -x "$(command -v exa)" ] && [ ! -x "$(command -v eza)" ] && [ ! -L ~/.local/bin/eza ] && ln -sf "$(command -v exa)" ~/.local/bin/eza

# Create XDG dirs
[ -d "${XDG_CACHE_HOME:-${HOME}/.cache}/zsh" ] || mkdir -p "${XDG_CACHE_HOME:-${HOME}/.cache}/zsh"
[ -d "${XDG_STATE_HOME:-${HOME}/.local/state}/zsh" ] || mkdir -p "${XDG_STATE_HOME:-${HOME}/.local/state}/zsh"

# history setup
HISTFILE="${XDG_STATE_HOME:-${HOME}/.local/state}/zsh/history"
SAVEHIST=50000
HISTSIZE=50000
setopt share_history 
setopt hist_expire_dups_first
setopt hist_ignore_dups
setopt hist_verify

# install, if not installed yet, and source zinit
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# load/install zinit plugins
[ -x "$(command -v fzf)" ] && zinit light Aloxaf/fzf-tab
[ -x "$(command -v fzf)" ] && zinit light Freed-Wu/fzf-tab-source
zinit light zsh-users/zsh-autosuggestions
zinit light zdharma-continuum/fast-syntax-highlighting
zinit ice depth=1
zinit light jeffreytse/zsh-vi-mode

# load/install Oh My Zsh plugins
zinit snippet OMZP::sudo/sudo.plugin.zsh

if [ -d /home/linuxbrew/.linuxbrew/share/zsh/site-functions ]; then
    zinit add-fpath /home/linuxbrew/.linuxbrew/share/zsh/site-functions/
fi

export SHELL_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}/shell"
[ -f "${SHELL_CONFIG_HOME}/functions.sh" ] && source "${SHELL_CONFIG_HOME}/functions.sh"
[ -f "${SHELL_CONFIG_HOME}/paths.sh" ] && source "${SHELL_CONFIG_HOME}/paths.sh"
[ -f "${SHELL_CONFIG_HOME}/envs.sh" ] && source "${SHELL_CONFIG_HOME}/envs.sh"
[ -f "${SHELL_CONFIG_HOME}/aliases.sh" ] && source "${SHELL_CONFIG_HOME}/aliases.sh"

# souce .zsh/*.zsh files
[[ ! -f $ZDOTDIR/plugins.zsh ]] || source $ZDOTDIR/plugins.zsh
[[ ! -f $ZDOTDIR/keybindings.zsh ]] || source $ZDOTDIR/keybindings.zsh

# compinit
autoload -U compinit
zstyle ':completion:*' cache-path "${XDG_CACHE_HOME:-${HOME}/.cache}/zsh/zcompcache"
compinit -d "${XDG_CACHE_HOME:-${HOME}/.cache}/zsh/zcompdump-${ZSH_VERSION}"

# load custom completions
[[ ! -f $ZDOTDIR/completions.zsh ]] || source $ZDOTDIR/completions.zsh

zinit cdreplay

$SCRIPTS_HOME/starship_install.sh

# create starship prompt
eval "$(starship init zsh)"
