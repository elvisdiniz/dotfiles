# Create some bin symbolic links: fdfind => fd, batcat => bat, exa => eza
[ -x "$(command -v fdfind)" ] && [ ! -L ~/.local/bin/fd ] && ln -sf "$(command -v fdfind)" ~/.local/bin/fd
[ -x "$(command -v batcat)" ] && [ ! -L ~/.local/bin/bat ] && ln -sf "$(command -v batcat)" ~/.local/bin/bat
[ -x "$(command -v exa)" ] && [ ! -x "$(command -v eza)" ] && [ ! -L ~/.local/bin/eza ] && ln -sf "$(command -v exa)" ~/.local/bin/eza

# Create XDG dirs
[ -d "${XDG_CACHE_HOME:-${HOME}/.cache}/zsh" ] || mkdir -p "${XDG_CACHE_HOME:-${HOME}/.cache}/zsh"
[ -d "${XDG_STATE_HOME:-${HOME}/.local/state}/zsh" ] || mkdir -p "${XDG_STATE_HOME:-${HOME}/.local/state}/zsh"

# history setup
HISTFILE="${XDG_STATE_HOME:-${HOME}/.local/state}/zsh/history"
SAVEHIST=50000
HISTSIZE=50000
setopt share_history 
setopt hist_expire_dups_first
setopt hist_ignore_dups
setopt hist_verify

# install, if not installed yet, and source zinit
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# load/install zinit plugins
[ -x "$(command -v fzf)" ] && zinit light Aloxaf/fzf-tab
zinit light zsh-users/zsh-autosuggestions
zinit light zdharma-continuum/fast-syntax-highlighting
zinit light asdf-vm/asdf
zinit ice depth=1
zinit light jeffreytse/zsh-vi-mode

# load/install Oh My Zsh plugins
zinit snippet OMZP::sudo/sudo.plugin.zsh

if [ -d /home/linuxbrew/.linuxbrew/share/zsh/site-functions ]; then
    zinit add-fpath /home/linuxbrew/.linuxbrew/share/zsh/site-functions/
fi

# source .shell/*.sh files
[[ ! -f ~/.shell/functions.sh ]] || source ~/.shell/functions.sh
[[ ! -f ~/.shell/paths.sh ]] || source ~/.shell/paths.sh
[[ ! -f ~/.shell/envs.sh ]] || source ~/.shell/envs.sh
[[ ! -f ~/.shell/aliases.sh ]] || source ~/.shell/aliases.sh

# souce .zsh/*.zsh files
[[ ! -f $ZDOTDIR/plugins.zsh ]] || source $ZDOTDIR/plugins.zsh
[[ ! -f $ZDOTDIR/keybindings.zsh ]] || source $ZDOTDIR/keybindings.zsh

# compinit
autoload -U compinit
zstyle ':completion:*' cache-path "${XDG_CACHE_HOME:-${HOME}/.cache}/zsh/zcompcache"
compinit -d "${XDG_CACHE_HOME:-${HOME}/.cache}/zsh/zcompdump-${ZSH_VERSION}"

# load custom completions
[[ ! -f $ZDOTDIR/completions.zsh ]] || source $ZDOTDIR/completions.zsh

zinit cdreplay

# check if starship is available on /usr/bin. If not, install on $BIN_HOME
if [ -x "/usr/bin/starship" ] || [ -x "/usr/local/bin/starship" ]; then
    [ -x "$BIN_HOME/starship" ] && rm "$BIN_HOME/starship"
else
    [ -x "$BIN_HOME/starship" ] || curl -sS https://starship.rs/install.sh | sh -s -- -b $BIN_HOME -y
fi

# create starship prompt
eval "$(starship init zsh)"
